.586
.MODEL FLAT, STDCALL
STD_OUTPUT_HANDLE equ -11
STD_INPUT_HANDLE equ -10
TIME_PERIODIC equ 1 
KEY_EV equ 1h
MOUSE_EV equ 2h
FOREGROUND_BLUE equ 1h 
FOREGROUND_GREEN equ 2h 
FOREGROUND_RED equ 4h 
FOREGROUND_INTENSITY equ 8h 
BACKGROUND_BLUE equ 10h 
BACKGROUND_GREEN equ 20h 
BACKGROUND_RED equ 40h 
BACKGROUND_INTENSITY equ 80h 
COL1 = 1h+8h 
EXTERN wsprintfA:NEAR
EXTERN GetStdHandle@4:NEAR
EXTERN WriteConsoleA@20:NEAR
EXTERN SetConsoleCursorPosition@8:NEAR
EXTERN SetConsoleTitleA@4:NEAR
EXTERN FreeConsole@0:NEAR
EXTERN AllocConsole@0:NEAR
EXTERN CharToOemA@8:NEAR
EXTERN SetConsoleTextAttribute@8:NEAR
EXTERN ReadConsoleA@20:NEAR
EXTERN timeSetEvent@20:NEAR
EXTERN timeKillEvent@4:NEAR
EXTERN ExitProcess@4:NEAR
includelib c:\masm32\lib\user32.lib
includelib c:\masm32\lib\kernel32.lib
includelib c:\masm32\lib\winmm.lib
COOR STRUC
X WORD ?
Y WORD ?
COOR ENDS
_DATA SEGMENT PARA PUBLIC USE32 'DATA';
HANDL DWORD ?
HANDL1 DWORD ?
STR2 DB "Пример таймера в консольном приложении",0
STR3 DB 100 dup(0)
FORM DB "Число вызовов таймера: %lu",0
BUF DB 200 dup(?)
NUM DWORD 0
LENS DWORD ? 
CRD COOR <?>
ID DWORD ? 
HWND DWORD ?
_DATA ENDS
_TEXT SEGMENT PARA PUBLIC USE32 'CODE';
START:
PUSH STD_INPUT_HANDLE
CALL GetStdHandle@4
MOV HANDL1,EAX
PUSH STD_OUTPUT_HANDLE
CALL GetStdHandle@4
MOV HANDL,EAX
PUSH OFFSET STR2
PUSH OFFSET STR2
CALL CharToOemA@8
PUSH OFFSET STR2
CALL SetConsoleTitleA@4
PUSH COL1
PUSH HANDL
CALL SetConsoleTextAttribute@8
PUSH TIME_PERIODIC
PUSH 0
PUSH OFFSET TIME 
PUSH 0
PUSH 1000 
CALL timeSetEvent@20
MOV ID,EAX
PUSH 0
PUSH OFFSET LENS
PUSH 200
PUSH OFFSET BUF
PUSH HANDL1
CALL ReadConsoleA@20
PUSH ID
CALL timeKillEvent@4
CALL FreeConsole@0
PUSH 0
CALL ExitProcess@4
LENSTR PROC
ENTER 0,0
PUSH EAX
CLD
MOV EDI,DWORD PTR [EBP+08H]
MOV EBX,EDI
MOV ECX,100 
XOR AL,AL
REPNE SCASB 
SUB EDI,EBX
MOV EBX,EDI
DEC EBX
POP EAX
LEAVE
RET 4
LENSTR ENDP
TIME PROC
PUSHA 
MOV CRD.X,15
MOV CRD.Y,15
PUSH CRD
PUSH HANDL
CALL SetConsoleCursorPosition@8
PUSH NUM
PUSH OFFSET FORM
PUSH OFFSET STR3
CALL wsprintfA
ADD ESP,12 
PUSH OFFSET STR3
PUSH OFFSET STR3
CALL CharToOemA@8
PUSH OFFSET STR3
CALL LENSTR
PUSH 0
PUSH OFFSET LENS
PUSH EBX
PUSH OFFSET STR3
PUSH HANDL
CALL WriteConsoleA@20
INC NUM
POPA
RET 20 
TIME ENDP
_TEXT ENDS
END START